//////////////////////////////////////////////////////////////////////////////// 
// Переменные
// 

&НаКлиенте
Перем АдресТоваровВХранилище;

//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

// Функция возвращает цену определенного товара на дату согласно виду цены
// 
// Параметры: 
//  Дата   – Дата – дата, на которую определяется цена. 
//  Товар  – СправочникСсылка.Товары – товар, цена которого определяется. 
//  ВидЦен – СправочникСсылка.ВидыЦен – вид цены. 
// 
// Возвращаемое значение: 
//  Число - Цена товара на определенную дату, согласно виду цены.
&НаСервереБезКонтекста
Функция ПолучитьЦенуТовара(Дата, Товар, ВидЦен)
	ЦенаТовара = РегистрыСведений.ЦеныТоваров.ПолучитьПоследнее(
		Дата, Новый Структура("Товар, ВидЦен", Товар, ВидЦен));
	Возврат ЦенаТовара.Цена;
КонецФункции

// Функция возвращает вид цены для указанного покупателя
// 
// Параметры: 
//  Покупатель – СправочникСсылка.Контрагенты – контрагент. 
// 
// Возвращаемое значение: 
//  СправочникСсылка.ВидыЦен - Вид цены для указанного покупателя.
&НаСервереБезКонтекста
Функция ПолучитьВидЦенПокупателя(Покупатель)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ВидЦен ИЗ Справочник.Контрагенты ГДЕ Ссылка = &Покупатель";
	Запрос.УстановитьПараметр("Покупатель", Покупатель);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВидЦен;
	КонецЕсли;
	Возврат Справочники.ВидыЦен.ПустаяСсылка();
КонецФункции

// Функция определяет услуга это или нет
&НаСервереБезКонтекста
Функция ЭтоУслуга(Товар)
	
	Возврат ?(Товар.Вид = Перечисления.ВидыТоваров.Услуга, Истина, Ложь);
	
КонецФункции

// Процедура устанавливает цены товаров и вычисляет суммы по каждой строке
// табличной части Товары.
// 
// Параметры: 
//  Нет.
// 
// Возвращаемое значение: 
//  Нет.
&НаСервере
Процедура ПересчитатьЦеныИСуммыТоваров(ПересчитатьДляВсехТоваров)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныТоваровСрезПоследних.Цена,
	               |	ЦеныТоваровСрезПоследних.Товар
	               |ИЗ
	               |	РегистрСведений.ЦеныТоваров.СрезПоследних(
	               |		,
	               |		ВидЦен = &ВидЦены
	               |			И Товар В (&Товары)) КАК ЦеныТоваровСрезПоследних";
	Запрос.УстановитьПараметр("ВидЦены", Объект.ВидЦен);
	Товары = Новый Массив();
	Для каждого Стр Из Объект.Товары Цикл 
		Товары.Добавить(Стр.Товар);
	КонецЦикла;
	Запрос.УстановитьПараметр("Товары", Товары);
	
	ТЗЦены = Запрос.Выполнить().Выгрузить();
	ТЗЦены.Индексы.Добавить("Товар");
	Для каждого Стр Из Объект.Товары Цикл 
		Если Стр.Цена = 0 ИЛИ ПересчитатьДляВсехТоваров Тогда
			ЦенаТовара = ТЗЦены.Найти(Стр.Товар, "Товар");
			Если ЦенаТовара <> Неопределено Тогда
				Стр.Цена = ЦенаТовара.Цена;
			Иначе 	
				Стр.Цена = 0;
			КонецЕсли;
		КонецЕсли;	
		Стр.Сумма = Стр.Цена * Стр.Количество;
		Стр.СуммаИзменена = Ложь;
		Стр.ЭтоУслуга = ЭтоУслуга(Стр.Товар);
	КонецЦикла;
КонецПроцедуры

// Функция помещает список товаров во временное хранилище и возвращает адрес 
&НаСервере
Функция ПоместитьТоварыВХранилище() 
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(,"Товар,Цена,Количество"), УникальныйИдентификатор);
КонецФункции	

// Функция восстанавливает список товаров из временного хранилища
&НаСервере
Процедура ПолучитьТоварыИзХранилища(АдресТоваровВХранилище)
	Объект.Товары.Загрузить(ПолучитьИзВременногоХранилища(АдресТоваровВХранилище));
	ПересчитатьЦеныИСуммыТоваров(Ложь);   
КонецПроцедуры	


// Функция возвращает ссылку на текущую строку в списке товаров 
// 
// Параметры: 
//  Нет. 
// 
// Возвращаемое значение: 
//  СправочникСсылка.Товары - Текущий товар в списке.
&НаКлиенте
Функция ПолучитьТекущуюСтрокуТовары()
	Возврат Элементы.Товары.ТекущиеДанные;
КонецФункции

// Процедура вычисляет дополнительные данные строки документа
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДополнительныеДанныеСтроки(Строка)
	
	Строка.СуммаИзменена = Строка.Сумма <> Строка.Количество * Строка.Цена;
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ 
// 

&НаКлиенте
Процедура ТоварыТоварПриИзменении(Элемент)
	Стр = ПолучитьТекущуюСтрокуТовары();
	Стр.ЭтоУслуга = ЭтоУслуга(Стр.Товар);
	Стр.Цена = ПолучитьЦенуТовара(Объект.Дата, Стр.Товар, Объект.ВидЦен);
	Стр.Количество = ?(Стр.ЭтоУслуга ИЛИ Стр.Количество = 0, 1, Стр.Количество); 

	dz_ПересчитатьСуммуСтрокиСУчетомСкидок();

КонецПроцедуры

&НаКлиенте
Процедура ПокупательПриИзменении(Элемент)
	ВидЦен = ПолучитьВидЦенПокупателя(Объект.Покупатель);
	Если Объект.ВидЦен <> ВидЦен Тогда
		Объект.ВидЦен = ВидЦен;
		Если Объект.Товары.Количество() > 0 Тогда
			ПересчитатьЦеныИСуммыТоваров(Истина);
		КонецЕсли;	
	КонецЕсли; 
	
	dz_ПересчитатьАвтоматическуюСкидку();

КонецПроцедуры

&НаКлиенте
Процедура ВидЦенПриИзменении(Элемент)
	Если Объект.Товары.Количество() > 0 Тогда
		ПересчитатьЦеныИСуммыТоваров(Истина);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	dz_ПересчитатьСуммуСтрокиСУчетомСкидок();	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент) 
	dz_ПересчитатьСуммуСтрокиСУчетомСкидок();	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	Стр = ПолучитьТекущуюСтрокуТовары();
	ЗаполнитьДополнительныеДанныеСтроки(Стр);
КонецПроцедуры

// Обработчик команды подбора
&НаКлиенте
Процедура КомандаПодбор()
#Если МобильныйКлиент Тогда
	Имя = "ОбщаяФорма.ФормаПодбораМобильная";
#Иначе
	Имя = "ОбщаяФорма.ФормаПодбора";
#КонецЕсли
	АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
	ПараметрыПодбора = Новый Структура("АдресТоваровДокумента, ВидЦен, Склад", АдресТоваровВХранилище, Объект.ВидЦен, Объект.Склад);
	ФормаПодбора = ОткрытьФорму(Имя, ПараметрыПодбора, ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНаСервере()
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Пересчитать();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	Для каждого Стр Из Объект.Товары Цикл
		
		ЗаполнитьДополнительныеДанныеСтроки(Стр);
		
	КонецЦикла
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВыполнить()
	ПересчитатьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОформитьДоставкуВыполнить()
	ПараметрыДоставки = Новый Структура("ДатаДокумента,Документ", Объект.Дата, Объект.Ссылка);
	ОткрытьФорму("Документ.РасходТовара.Форма.ОформлениеДоставки", ПараметрыДоставки);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПараметрыОпций = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда 
		
		ПараметрыОпций = Новый Структура("Организация", Объект.Организация);
		УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);
		
	КонецЕсли;
	
	Для каждого Стр Из Объект.Товары Цикл
		
		ЗаполнитьДополнительныеДанныеСтроки(Стр);
		
	КонецЦикла;  
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПараметрыОпций = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);

	Для каждого Стр Из Объект.Товары Цикл
		
		ЗаполнитьДополнительныеДанныеСтроки(Стр);
		Стр.ЭтоУслуга = ЭтоУслуга(Стр.Товар);
		
	КонецЦикла;
	 
	dz_УстановитьВидимостьЭлементов(); //***
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыОпций = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);
	
	Для каждого Стр Из Объект.Товары Цикл
		
		ЗаполнитьДополнительныеДанныеСтроки(Стр);
		
	КонецЦикла
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПодбор() Экспорт
	
	ПолучитьТоварыИзХранилища(АдресТоваровВХранилище);  
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.Покупатель = НовыйОбъект;
		ВидЦен = ПолучитьВидЦенПокупателя(Объект.Покупатель);
		Если Объект.ВидЦен <> ВидЦен Тогда
			Объект.ВидЦен = ВидЦен;
			Если Объект.Товары.Количество() > 0 Тогда
				ПересчитатьЦеныИСуммыТоваров(Истина);
			КонецЕсли;	
		КонецЕсли;
		ТекущийЭлемент = Элементы.Покупатель;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция dz_ДоступныеСкидки(ДатаДокумента)
	
	ДоступныеСкидки = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	dz_ДействующиеСкидкиСрезПоследних.Скидка КАК Скидка,
		|	dz_ДействующиеСкидкиСрезПоследних.СрокДействияСкидки КАК СрокДействияСкидки
		|ИЗ
		|	РегистрСведений.dz_ДействующиеСкидки.СрезПоследних(&ПериодСкидки, ) КАК dz_ДействующиеСкидкиСрезПоследних
		|ГДЕ
		|	dz_ДействующиеСкидкиСрезПоследних.Действует = ИСТИНА";
	
	Запрос.УстановитьПараметр("ПериодСкидки", ДатаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СрокДействияСкидки)
			И ДатаДокумента > ВыборкаДетальныеЗаписи.СрокДействияСкидки
			Тогда
			Продолжить;
		КонецЕсли;	   
		
		ДоступныеСкидки.Добавить(ВыборкаДетальныеЗаписи.Скидка);
	КонецЦикла;
	
	Возврат ДоступныеСкидки;
	
КонецФункции

&НаКлиенте
Процедура dz_СкидкаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;   
	ДанныеВыбора 		 = dz_ДоступныеСкидки(Объект.Дата);
КонецПроцедуры

&НаСервере
Процедура dz_УстановитьВидимостьЭлементов()
	
	УчетСкидок = ПолучитьФункциональнуюОпцию("dz_УчетСкидок");
	Если УчетСкидок Тогда
		ВидимостьСкидок = Истина;		
	Иначе
		ВидимостьСкидок = Объект.dz_СуммаСкидки > 0;
	КонецЕсли;	
	Элементы.dz_ТоварыГруппаСкидкаРучная.Видимость 			= ВидимостьСкидок;
	Элементы.dz_ТоварыГруппаСкидкаАвтоматическая.Видимость  = ВидимостьСкидок;
	
КонецПроцедуры

&НаСервере
Процедура dz_ПересчитатьАвтоматическуюСкидку()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	dz_ОбъемЗакупокПоНакопительнойКартеОбороты.НакопительнаяКарта КАК НакопительнаяКарта,
		|	СУММА(dz_ОбъемЗакупокПоНакопительнойКартеОбороты.СуммаЗакупокОборот) КАК СуммаЗакупокОборот
		|ПОМЕСТИТЬ ВТ_СуммаЗакупок
		|ИЗ
		|	РегистрНакопления.dz_ОбъемЗакупокПоНакопительнойКарте.Обороты(, &ПериодСкидки, Регистратор, НакопительнаяКарта = &НакопительнаяКарта) КАК dz_ОбъемЗакупокПоНакопительнойКартеОбороты
		|ГДЕ
		|	dz_ОбъемЗакупокПоНакопительнойКартеОбороты.Регистратор <> &ТекущийДокумент
		|
		|СГРУППИРОВАТЬ ПО
		|	dz_ОбъемЗакупокПоНакопительнойКартеОбороты.НакопительнаяКарта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	dz_Скидки.ТипРасчетаСкидки КАК ТипРасчетаСкидки,
		|	dz_Скидки.ТипСкидки КАК ТипСкидки,
		|	dz_ПорогиРасчетаСкидкиСрезПоследних.ЗначениеСкидки КАК ЗначениеСкидки,
		|	dz_ПорогиРасчетаСкидкиСрезПоследних.Скидка КАК Скидка,
		|	dz_ПорогиРасчетаСкидкиСрезПоследних.ПорогПрименения КАК ПорогПрименения
		|ПОМЕСТИТЬ ВТ_ПорогипримененияСкидки
		|ИЗ
		|	РегистрСведений.dz_ПорогиРасчетаСкидки.СрезПоследних(&ПериодСкидки, Скидка = &Скидка) КАК dz_ПорогиРасчетаСкидкиСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.dz_Скидки КАК dz_Скидки
		|		ПО dz_ПорогиРасчетаСкидкиСрезПоследних.Скидка = dz_Скидки.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВТ_ПорогипримененияСкидки.ЗначениеСкидки) КАК ЗначениеСкидки,
		|	ВТ_ПорогипримененияСкидки.ТипСкидки КАК ТипСкидки
		|ИЗ
		|	ВТ_ПорогипримененияСкидки КАК ВТ_ПорогипримененияСкидки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммаЗакупок КАК ВТ_СуммаЗакупок
		|		ПО (ВТ_ПорогипримененияСкидки.ПорогПрименения <= ЕСТЬNULL(ВТ_СуммаЗакупок.СуммаЗакупокОборот, 0))
		|ГДЕ
		|	ВТ_ПорогипримененияСкидки.ЗначениеСкидки > 0
		|	И ВТ_ПорогипримененияСкидки.ТипРасчетаСкидки = &ТипРасчетаСкидки
		|	И ВТ_ПорогипримененияСкидки.ПорогПрименения <= ЕСТЬNULL(ВТ_СуммаЗакупок.СуммаЗакупокОборот, 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПорогипримененияСкидки.ТипСкидки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВТ_ПорогипримененияСкидки.ЗначениеСкидки),
		|	ВТ_ПорогипримененияСкидки.ТипСкидки
		|ИЗ
		|	ВТ_ПорогипримененияСкидки КАК ВТ_ПорогипримененияСкидки
		|ГДЕ
		|	ВТ_ПорогипримененияСкидки.ТипРасчетаСкидки <> &ТипРасчетаСкидки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПорогипримененияСкидки.ТипСкидки";
	
	Запрос.УстановитьПараметр("НакопительнаяКарта", Объект.dz_КартаЛояльности);
	Запрос.УстановитьПараметр("ПериодСкидки", 		Объект.Дата);
	Запрос.УстановитьПараметр("Скидка", 			Объект.dz_Скидка);
	Запрос.УстановитьПараметр("ТекущийДокумент", 	Объект.Ссылка);
	Запрос.УстановитьПараметр("ТипРасчетаСкидки", 	Перечисления.dz_ТипыРасчетаСкидки.ПоКартеЛояльности);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	РасчетПоПроценту 	= Ложь;
	ЗначениеСкидки		= 0;
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗначениеСкидки = ВыборкаДетальныеЗаписи.ЗначениеСкидки;
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТипСкидки)
			И ВыборкаДетальныеЗаписи.ТипСкидки = Перечисления.dz_ТипыСкидок.Процент 
			Тогда
			РасчетПоПроценту = Истина;
		КонецЕсли;	
	КонецЕсли;         
		
	Для Каждого СтрТовары Из Объект.Товары Цикл
		
		СуммаБезСкидки 				= Окр(СтрТовары.Количество * СтрТовары.Цена, 2); 
		
		Если СуммаБезСкидки = 0 Тогда
			СтрТовары.dz_СуммаСкидкиАвтоматической 		= 0;
			СтрТовары.dz_СуммаСкидкиРучной 				= 0;			
			СтрТовары.dz_ПроцентСкидкиАвтоматической 	= 0;
			СтрТовары.dz_ПроцентСкидкиРучной 			= 0;
			
			Продолжить;
		КонецЕсли;	
		
		Если РасчетПоПроценту Тогда
			ПроцентАвтоматическойСкидки 			 = ЗначениеСкидки;
			СтрТовары.dz_СуммаСкидкиАвтоматической	 = Мин(Окр(СуммаБезСкидки * ПроцентАвтоматическойСкидки / 100, 2), СуммаБезСкидки);
		Иначе
			СтрТовары.dz_СуммаСкидкиАвтоматической	 = Мин(ЗначениеСкидки, СуммаБезСкидки);
		КонецЕсли;	

		СтрТовары.dz_ПроцентСкидкиАвтоматической = Окр(СтрТовары.dz_СуммаСкидкиАвтоматической / СуммаБезСкидки * 100, 0);  
		
		//Пересчитать ручную скидку
		СуммаСкидкиРучной 					= Мин(СтрТовары.dz_СуммаСкидкиРучной, СуммаБезСкидки-СтрТовары.dz_СуммаСкидкиАвтоматической); 
		СтрТовары.dz_СуммаСкидкиРучной 		= Макс(СуммаСкидкиРучной, 0);		
		СтрТовары.dz_ПроцентСкидкиРучной 	= Окр(СтрТовары.dz_СуммаСкидкиРучной / СуммаБезСкидки * 100, 0);  		
		///////////////////////////                                                                           
		СтрТовары.Сумма = СуммаБезСкидки - (СтрТовары.dz_СуммаСкидкиАвтоматической + СтрТовары.dz_СуммаСкидкиРучной);
		
		ЗаполнитьДополнительныеДанныеСтроки(СтрТовары);
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Функция ПараметрыРасчетаСкидки()

	ПараметрыРасчетаСкидки = Новый Структура();
	ПараметрыРасчетаСкидки.Вставить("НакопительнаяКарта", 	Объект.dz_КартаЛояльности);
	ПараметрыРасчетаСкидки.Вставить("ПериодСкидки", 		Объект.Дата);
	ПараметрыРасчетаСкидки.Вставить("Скидка", 				Объект.dz_Скидка);
	ПараметрыРасчетаСкидки.Вставить("ТекущийДокумент", 		Объект.Ссылка);	
	
КонецФункции

&НаКлиенте
Процедура dz_СкидкаПриИзменении(Элемент)  
	
	dz_ПересчитатьАвтоматическуюСкидку();	
	
КонецПроцедуры

&НаКлиенте
Процедура dz_КартаЛояльностиПриИзменении(Элемент)
	dz_ПересчитатьАвтоматическуюСкидку();
КонецПроцедуры


&НаКлиенте
Процедура Товарыdz_ПроцентСкидкиРучнойПриИзменении(Элемент)
	dz_ПересчитатьСуммуСтрокиСУчетомСкидок(Истина);
КонецПроцедуры


&НаКлиенте
Процедура Товарыdz_СуммаСкидкиРучнойПриИзменении(Элемент)
	dz_ПересчитатьСуммуСтрокиСУчетомСкидок();
КонецПроцедуры

&НаКлиенте
Процедура dz_ПересчитатьСуммуСтрокиСУчетомСкидок(РасчетПоПроценту = Ложь)
	СтрТовары = ПолучитьТекущуюСтрокуТовары();
	
	СуммаБезСкидки 						= Окр(СтрТовары.Количество * СтрТовары.Цена, 2); 

	//Пересчитать ручную скидку    
	Если РасчетПоПроценту Тогда 
		СуммаСкидкиРучной = Мин(Окр(СуммаБезСкидки * СтрТовары.dz_ПроцентСкидкиРучной / 100, 2) , СуммаБезСкидки-СтрТовары.dz_СуммаСкидкиАвтоматической); 
	Иначе
		СуммаСкидкиРучной = Мин(СтрТовары.dz_СуммаСкидкиРучной, СуммаБезСкидки-СтрТовары.dz_СуммаСкидкиАвтоматической); 
	КонецЕсли;	

	СтрТовары.dz_СуммаСкидкиРучной 		= Макс(СуммаСкидкиРучной, 0);	
	Если СтрТовары.dz_СуммаСкидкиРучной = 0 
		Или СуммаБезСкидки = 0
		Тогда
		СтрТовары.dz_ПроцентСкидкиРучной 	= 0;
	Иначе	
		СтрТовары.dz_ПроцентСкидкиРучной 	= Окр(СтрТовары.dz_СуммаСкидкиРучной / СуммаБезСкидки * 100, 0);  		
    КонецЕсли;
	//Пересчитаем сумму
	СтрТовары.Сумма = СуммаБезСкидки - (СтрТовары.dz_СуммаСкидкиАвтоматической + СтрТовары.dz_СуммаСкидкиРучной);	
	
	ЗаполнитьДополнительныеДанныеСтроки(СтрТовары);
КонецПроцедуры

