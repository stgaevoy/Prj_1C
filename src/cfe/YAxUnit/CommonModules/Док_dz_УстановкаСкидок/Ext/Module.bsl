
Процедура ИсполняемыеСценарии() Экспорт
    
    ЮТТесты.ДобавитьТестовыйНабор("Документ Установка скидок: Проверка проведения").ВТранзакции()
		.ДобавитьТест("ПроверкаПроведенияПозитив")
		.ДобавитьТест("ПроверкаПроведенияНегатив")
		;

КонецПроцедуры

Процедура ПроверкаПроведенияПозитив() Экспорт
	
	// Описание программного создания: https://bia-technologies.github.io/yaxunit/docs/features/test-data/data-generation/
	
	ТипРасчетаСкидкиПроцент = Перечисления.dz_ТипыРасчетаСкидки.Фиксированная;
	ТипСкидкиПроцент 		= Перечисления.dz_ТипыСкидок.Процент;
	НаименованиеСкидки		= "СкидкаТест";
	
	// Создание скидки
	СкидкаНовая = СозданиеСкидки(НаименованиеСкидки, ТипРасчетаСкидкиПроцент, ТипСкидкиПроцент);
		
	// Создание Установки скидки
	НижняяГраницаПорога = 0;
	ЗначениеСкидки 		= 5;
	
	УстановкаСкидок = ЮТест.Данные().КонструкторОбъекта("Документы.dz_УстановкаСкидок")
		.ФикцияОбязательныхПолей()
		.Установить("Дата", ТекущаяДатаСеанса())
		.Установить("Скидка", СкидкаНовая)
		.Установить("ПериодДействияНачало", ДобавитьМесяц(ТекущаяДатаСеанса(), -1))
		.Установить("ПериодДействияКонец", ДобавитьМесяц(ТекущаяДатаСеанса(), 1))
		.ТабличнаяЧасть("ПорогПримененияСкидки").ДобавитьСтроку()
			.Установить("НижняяГраницаПорога", НижняяГраницаПорога)
			.Установить("ЗначениеСкидки", ЗначениеСкидки)
		.Записать() // формируем документ ссылку не проведенную
		;
		
	// Проверка метода проведения, в случае выполнения: УстановкаСкидок.ПолучитьОбъект().Записать(РежимЗаписиДокумента.Проведение)
	// Описание проверки метода: https://bia-technologies.github.io/yaxunit/docs/features/assertions/assertions-base#%D0%BF%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%BA%D0%B0-%D0%B2%D1%8B%D0%BF%D0%BE%D0%BB%D0%BD%D0%B5%D0%BD%D0%B8%D1%8F-%D0%BC%D0%B5%D1%82%D0%BE%D0%B4%D0%B0
	
	
	ЮТест.ОжидаетЧто(УстановкаСкидок.ПолучитьОбъект())  // Объект, метод которого проверяем
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)   // Указываем проверяемый метод и параметры вызова
		.НеВыбрасываетИсключение()	
		;

КонецПроцедуры

Процедура ПроверкаПроведенияНегатив() Экспорт
	
 	// Описание программного создания: https://bia-technologies.github.io/yaxunit/docs/features/test-data/data-generation/
	
	ТипРасчетаСкидкиПроцент = Перечисления.dz_ТипыРасчетаСкидки.Фиксированная;
	ТипСкидкиПроцент 		= Перечисления.dz_ТипыСкидок.Процент;
	НаименованиеСкидки		= "СкидкаТест";
	
	// Создание скидки
	СкидкаНовая = СозданиеСкидки(НаименованиеСкидки, ТипРасчетаСкидкиПроцент, ТипСкидкиПроцент);
		
	// Создание Установки скидки
	УстановкаСкидок = ЮТест.Данные().КонструкторОбъекта("Документы.dz_УстановкаСкидок")
		.ФикцияОбязательныхПолей()
		.Установить("Дата", ТекущаяДатаСеанса())
		.Установить("Скидка", СкидкаНовая)
		.Установить("ПериодДействияНачало", ДобавитьМесяц(ТекущаяДатаСеанса(), -1))
		.Установить("ПериодДействияКонец", ДобавитьМесяц(ТекущаяДатаСеанса(), 1))
		.Записать() // формируем документ ссылку не проведенную
		;
		
	// Проверка метода проведения, в случае выполнения: УстановкаСкидок.ПолучитьОбъект().Записать(РежимЗаписиДокумента.Проведение)
	// Описание проверки метода: https://bia-technologies.github.io/yaxunit/docs/features/assertions/assertions-base#%D0%BF%D1%80%D0%BE%D0%B2%D0%B5%D1%80%D0%BA%D0%B0-%D0%B2%D1%8B%D0%BF%D0%BE%D0%BB%D0%BD%D0%B5%D0%BD%D0%B8%D1%8F-%D0%BC%D0%B5%D1%82%D0%BE%D0%B4%D0%B0
	
	
	ЮТест.ОжидаетЧто(УстановкаСкидок.ПолучитьОбъект())  // Объект, метод которого проверяем
		.Метод("Записать",  ЮТМетоды.МассивПараметров(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный)) // Указываем проверяемый метод и параметры вызова
		.ВыбрасываетИсключение("Не удалось провести ""Установка скидок")	
		;
		
	Сообщения = ПолучитьСообщенияПользователю();
	ЮТест.ОжидаетЧто(Сообщения)
		.ИмеетТип("ФиксированныйМассив")
		.ИмеетДлину(1)
		.Свойство("[0].Текст")
			.ИмеетТип("Строка")
			.Содержит("Пороги применения скидки должны быть заполнены")
		;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Функция СозданиеСкидки(Наименование, ТипРасчетаСкидки, ТипСкидки)
			
	КонструкторСкидки = ЮТест.Данные().КонструкторОбъекта("Справочники.dz_Скидки")
		.ФикцияОбязательныхПолей() 
		.Установить("Наименование", Наименование)
		.Установить("ТипРасчетаСкидки", ТипРасчетаСкидки)
		.Установить("ТипСкидки", ТипСкидки)
		;
    СкидкаНовая = КонструкторСкидки.Записать();
	
	Возврат СкидкаНовая;

КонецФункции
 

#КонецОбласти